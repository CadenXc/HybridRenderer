#version 460
#extension GL_EXT_nonuniform_qualifier : require
#extension GL_EXT_scalar_block_layout : enable
#extension GL_GOOGLE_include_directive : require

#include "ShaderCommon.h"

layout(local_size_x = 8, local_size_y = 8) in;

layout(binding = 0, set = 0) uniform CameraProperties {
    UniformBufferObject cam;
};

// SVGF_Atrous Layout
layout(binding = 0, set = 1, rgba16f) uniform image2D imgNormal;
layout(binding = 1, set = 1)          uniform sampler2D samplerDepth;
layout(binding = 2, set = 1, rgba16f) uniform image2D imgInput;
layout(binding = 3, set = 1, rgba16f) uniform image2D outFiltered;

layout(push_constant) uniform PushConstants {
    int stepSize;
} push;

void main() {
    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
    if (any(greaterThanEqual(pixel, ivec2(cam.displaySize)))) return;

    vec3 centerNormal = imageLoad(imgNormal, pixel).rgb * 2.0 - 1.0;
    float centerDepth = texelFetch(samplerDepth, pixel, 0).r;
    vec4 centerColor = imageLoad(imgInput, pixel);

    if (centerDepth == 1.0) {
        imageStore(outFiltered, pixel, centerColor);
        return;
    }

    vec4 sumColor = vec4(0.0);
    float sumWeight = 0.0;

    // Edge-stopping weights parameters
    float phiColor = 10.0;
    float phiNormal = 128.0;
    float phiDepth = 1.0;

    int step = push.stepSize;

    for (int y = -2; y <= 2; y++) {
        for (int x = -2; x <= 2; x++) {
            ivec2 offset = ivec2(x, y) * step;
            ivec2 samplePixel = pixel + offset;

            if (any(lessThan(samplePixel, ivec2(0))) || any(greaterThanEqual(samplePixel, ivec2(cam.displaySize)))) continue;

            vec3 sampleNormal = imageLoad(imgNormal, samplePixel).rgb * 2.0 - 1.0;
            float sampleDepth = texelFetch(samplerDepth, samplePixel, 0).r;
            vec4 sampleColor = imageLoad(imgInput, samplePixel);

            // Compute weights
            float wNormal = pow(max(0.0, dot(centerNormal, sampleNormal)), phiNormal);
            float wDepth = abs(centerDepth - sampleDepth) / phiDepth;
            float wColor = length(centerColor.rgb - sampleColor.rgb) / phiColor;

            float weight = exp(-wColor - wDepth) * wNormal;

            sumColor += sampleColor * weight;
            sumWeight += weight;
        }
    }

    imageStore(outFiltered, pixel, sumColor / max(sumWeight, 0.0001));
}
