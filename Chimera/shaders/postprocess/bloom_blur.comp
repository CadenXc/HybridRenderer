#version 460
#include "ShaderCommon.h"

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0) uniform GlobalUBO 
{
    UniformBufferObject ubo;
} global;

layout(set = 2, binding = 0) uniform sampler2D inputColor;
layout(set = 2, binding = 1, rgba16f) uniform image2D outputColor;

layout(push_constant) uniform PushConstants 
{
    int horizontal; // 1 for horizontal, 0 for vertical
} pc;

void main() 
{
    ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);
    if (pixel.x >= global.ubo.displaySize.x || pixel.y >= global.ubo.displaySize.y) 
    {
        return;
    }

    vec2 uv = (vec2(pixel) + 0.5) / global.ubo.displaySize;
    
    float weight[5] = float[](0.227027, 0.1945946, 0.1216216, 0.054054, 0.016216);
    vec3 result = texture(inputColor, uv).rgb * weight[0];

    vec2 tex_offset = global.ubo.displaySizeInverse;

    if (pc.horizontal != 0) 
    {
        for (int i = 1; i < 5; ++i) 
        {
            result += texture(inputColor, uv + vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
            result += texture(inputColor, uv - vec2(tex_offset.x * i, 0.0)).rgb * weight[i];
        }
    } 
    else 
    {
        for (int i = 1; i < 5; ++i) 
        {
            result += texture(inputColor, uv + vec2(0.0, tex_offset.y * i)).rgb * weight[i];
            result += texture(inputColor, uv - vec2(0.0, tex_offset.y * i)).rgb * weight[i];
        }
    }

    imageStore(outputColor, pixel, vec4(result, 1.0));
}
