#version 450
#extension GL_GOOGLE_include_directive : require
#include "ShaderCommon.h"

layout(local_size_x = 16, local_size_y = 16) in;

layout(set = 0, binding = 0) uniform GlobalUBO {
    UniformBufferObject cam;
} ubo;

layout(set = 2, binding = 0) uniform sampler2D depthTexture;
layout(set = 2, binding = 1, rgba8) uniform writeonly image2D outLinearDepth;

layout(push_constant) uniform PushConstants {
    float scale;
} pc;

void main() {
    ivec2 texelCoord = ivec2(gl_GlobalInvocationID.xy);
    ivec2 size = textureSize(depthTexture, 0);

    if (texelCoord.x >= size.x || texelCoord.y >= size.y) return;

    vec2 uv = vec2(texelCoord + 0.5) / vec2(size);
    float depth = texture(depthTexture, uv).r;
    
    // NDC coordinates
    vec4 ndc = vec4(uv * 2.0 - 1.0, depth, 1.0);
    // Vulkan Y is inverted in NDC for GLM
    ndc.y *= -1.0; 

    vec4 viewPos = ubo.cam.projInverse * ndc;
    viewPos /= viewPos.w;
    
    float linearDepth = -viewPos.z; // In eye space, Z is negative
    
    // For visualization, we need to map this to [0, 1]
    float visualized = clamp(linearDepth / pc.scale, 0.0, 1.0);

    imageStore(outLinearDepth, texelCoord, vec4(vec3(visualized), 1.0));
}